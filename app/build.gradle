apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}


android {
    signingConfigs {
        release_config {
            /*
             * File - Project Structure - Modules(app) - Signing - release_config 수정
             * 인증서 내용을 미리 스크립트로 작성
             * Github 에 올리기전 패스워드 여부 확인!
             */
//            keyAlias 'zeropage'
//            keyPassword '비밀번호 입력 필수'
//            storeFile file('파일경로 필수')
//            storePassword '비밀번호 입력 필수'
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.zeropage.apps.zeropage"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //상단 release_config 내용 수정 후 하단 주석 해제할 것
        //signingConfig signingConfigs.release_config
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            //상단 release_config 내용 수정 후 하단 주석 해제할 것
            //signingConfig signingConfigs.release_config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
        //Flavor 설정시 어플리케이션ID가 다를 경우, FireBase 콘솔가서 앱 등록을 새로 해줘야함
        mainTestConfig {
            applicationId 'org.zeropage.apps'
            minSdkVersion 19
            targetSdkVersion 25
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            applicationIdSuffix ".mainTestConfig"
            versionNameSuffix "-mainTestConfig"
        }
        logDelConfig {
            applicationId "org.zeropage.apps"
            minSdkVersion 19
            targetSdkVersion 25
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            applicationIdSuffix ".logDelConfig"
            versionNameSuffix "-logDelConfig"
        }
    }

    sourceSets {
        mainTestConfig.java.srcDirs('src/mainTestConfig/java')
        logDelConfig.java.srcDirs('src/logDelConfig/java')
    }

    /* apk 추출시 파일명 자동 생성(FlavorName_BuildType_날짜_시간.apk)
     */
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def date = new Date();
            def formatDate = date.format("yyMMdd")
            def formatTime = date.format("HHmmss")
            def file = output.outputFile
            output.outputFile = new File(file.parent,
                    variant.getFlavorName() + "_" + variant.getBuildType().getName()
                            + "_" + formatDate + "_" + formatTime + ".apk")
        }
    }
}

/* google service 관련 라이브러리는 firebase 버전보다 낮거나 같아야함
 * FireBase 10.2.1 기준으로 진행
 */
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.0.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-crash:10.2.1'
    compile 'com.google.firebase:firebase-messaging:10.2.1'
    compile 'com.jrummyapps:animated-svg-view:1.0.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.android.gms:play-services-auth:10.2.1'
    androidTestCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
